ITERATIONS = 1000
TIME_STEP = 5e-06
VACANCY_SEARCH_DISTANCE = 100
 
INCLUDE_FILE = "fceri_ji_comp.geometry.mdl"

MODIFY_SURFACE_REGIONS
{
   EC[wall] {
      SURFACE_CLASS = reflect
   }
   EC[ALL] {
      SURFACE_CLASS = reflect
   }
   CP[PM] {
      SURFACE_CLASS = reflect
   }
   CP[ALL] {
      SURFACE_CLASS = reflect
   }
}

DEFINE_SURFACE_CLASSES
{
   reflect {
   REFLECTIVE = ALL_MOLECULES
   }
   reflecto { 
   REFLECTIVE = ALL_MOLECULES
   }
   reflectop { 
   REFLECTIVE = ALL_MOLECULES
   }
}
 
/* Model Parameters */
   Nav = 6.022e8               /* Avogadro number based on a volume size of 1 cubic um */
   rxn_layer_t = 0.01
   vol_wall = 0.88/rxn_layer_t  /*Surface area*/
   vol_EC = 39
   vol_PM = 0.01/rxn_layer_t  /*Surface area*/
   vol_CP = 1
   Lig_tot = 6.0e3
   Rec_tot = 4.0e2
   Lyn_tot = 2.8e2
   Syk_tot = 4e2
   kp1 = 0.000166057788110262*Nav
   km1 = 0.00
   kp2 = 1.66057788110262e-06/rxn_layer_t
   km2 = 0.00
   kpL = 0.0166057788110262/rxn_layer_t
   kmL = 20
   kpLs = 0.0166057788110262/rxn_layer_t
   kmLs = 0.12
   kpS = 0.0166057788110262*Nav
   kmS = 0.13
   pLb = 30
   pLbs = 100
   pLg = 1
   pLgs = 3
   pLS = 30
   pLSs = 100
   pSS = 100
   pSSs = 200
   dm = 0.1
   dc = 0.1

/* Diffusion bloc */
   T = 298.15      /* Temperature, K */
   h = rxn_layer_t      /* Thickness of 2D compartment, um */
   Rs = 0.002564      /* Radius of a (spherical) molecule in 3D compartment, um */
   Rc = 0.0015      /* Radius of a (cylindrical) molecule in 2D compartment, um */
   gamma = 0.5722      /* Euler's constant */
   KB = 1.3806488e-19     /* Boltzmann constant, cm^2.kg/K.s^2 */
   mu_wall = 1e-9      /* Viscosity in compartment wall, kg/um.s */
   mu_EC = 1e-9      /* Viscosity in compartment EC, kg/um.s */
   mu_PM = 1e-9      /* Viscosity in compartment PM, kg/um.s */
   mu_CP = 1e-9      /* Viscosity in compartment CP, kg/um.s */
 


#DEFINE_MOLECULES
{
  Lig(l,l)
  {
      DIFFUSION_CONSTANT_3D = 8.51e-7 
  }
  Lyn(U,SH2)
  {
      DIFFUSION_CONSTANT_2D =  1.7e-7 
  }
  Syk(tSH2,l~Y~pY,a~Y~pY)
  {
      DIFFUSION_CONSTANT_3D = 8.51e-7 
  }
  Rec(a,b~Y~pY,g~Y~pY)
  {
      DIFFUSION_CONSTANT_2D =  1.7e-7
  }
}



#DEFINE_REACTIONS
{
    /* Ligand-receptor binding      */
     Rec(a) + Lig(l,l) <-> Rec(a!1).Lig(l!1,l)  [kp1, km1]

    /* Receptor-aggregation*/
     Rec(a) + Lig(l,l!+) <-> Rec(a!2).Lig(l!2,l!+)  [kp2, km2]

    /* Constitutive Lyn-receptor binding  */
     Rec(b~Y) + Lyn(U,SH2) <-> Rec(b~Y!1).Lyn(U!1,SH2)  [kpL, kmL]

    /* Transphosphorylation of beta by constitutive Lyn  */
     Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~Y) -> Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,b~pY)  [pLb]

    /* Transphosphorylation of gamma by constitutive Lyn  */
     Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~Y) -> Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY)  [pLg]

    /* Lyn-receptor binding through SH2 domain  */
     Rec(b~pY) + Lyn(U,SH2) <-> Rec(b~pY!1).Lyn(U,SH2!1)  [kpLs, kmLs]

    /* Transphosphorylation of beta by SH2-bound Lyn  */
     Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,b~Y) -> Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,b~pY)  [pLbs]

    /* Transphosphorylation of gamma by SH2-bound Lyn  */
     Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~Y) -> Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY)  [pLgs]

    /* Syk-receptor binding through tSH2 domain  */
     Rec(g~pY) + Syk(tSH2) <-> Rec(g~pY!1).Syk(tSH2!1)  [kpS, kmS]

    /* Transphosphorylation of Syk by constitutive Lyn  */
     Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~Y) -> Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b~Y!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~pY)  [pLS]

    /* Transphosphorylation of Syk by SH2-bound Lyn  */
     Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~Y) -> Lig(l!1,l!2).Lyn(U,SH2!3).Rec(a!2,b~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,l~pY)  [pLSs]

    /* Transphosphorylation of Syk by Syk not phosphorylated on aloop  */
     Lig(l!1,l!2).Syk(tSH2!3,a~Y).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~Y) -> Lig(l!1,l!2).Syk(tSH2!3,a~Y).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~pY)  [pSS]

    /* Transphosphorylation of Syk by Syk phosphorylated on aloop  */
     Lig(l!1,l!2).Syk(tSH2!3,a~pY).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~Y) -> Lig(l!1,l!2).Syk(tSH2!3,a~pY).Rec(a!2,g~pY!3).Rec(a!1,g~pY!4).Syk(tSH2!4,a~pY)  [pSSs]

    /* Dephosphorylation of Rec beta  */
     Rec(b~pY) -> Rec(b~Y)  [dm]

    /* Dephosphorylation of Rec gamma  */
     Rec(g~pY) -> Rec(g~Y)  [dm]

    /* Dephosphorylation of Syk at membrane  */
     Syk(tSH2!+,l~pY) -> Syk(tSH2!+,l~Y)  [dm]
     Syk(tSH2!+,a~pY) -> Syk(tSH2!+,a~Y)  [dm]

    /* Dephosphorylation of Syk in cytosol  */
     Syk(tSH2,l~pY) -> Syk(tSH2,l~Y)  [dc]
     Syk(tSH2,a~pY) -> Syk(tSH2,a~Y)  [dc]
}


#INSTANTIATE Scene OBJECT
{
  EC OBJECT EC {}
  CP OBJECT CP {
    PARENT = EC
    MEMBRANE = PM OBJECT CP[ALL]
  }

   ligand_rel RELEASE_SITE
   {
    SHAPE = Scene.EC[ALL] - Scene.CP[ALL]
    MOLECULE = @EC:Lig(l,l)
    NUMBER_TO_RELEASE = Lig_tot
    RELEASE_PROBABILITY = 1
   }
   lyn_rel RELEASE_SITE
   {
    SHAPE = Scene.CP[PM]
    MOLECULE = @PM:Lyn(U,SH2)
    NUMBER_TO_RELEASE = Lyn_tot
    RELEASE_PROBABILITY = 1
   }
   syk_rel RELEASE_SITE
   {
    SHAPE = Scene.CP
    MOLECULE = @CP:Syk(tSH2,l~Y,a~Y)
    NUMBER_TO_RELEASE = Syk_tot
    RELEASE_PROBABILITY = 1
   }
   receptor_rel RELEASE_SITE
   {
    SHAPE = Scene.CP[PM]
    MOLECULE = @PM:Rec(a,b~Y,g~Y)
    NUMBER_TO_RELEASE = Rec_tot
    RELEASE_PROBABILITY = 1
   }

}
 
/* Observables bloc */
#REACTION_DATA_OUTPUT
{
   STEP = 1e-6

    /*LynFree*/
    {COUNT[Lyn(U,SH2), WORLD] } => "./react_data/LycFree.dat"
    
    //{COUNT[Rec.Rec, WORLD] } => "./react_data/RecDim.dat"
    //COUNT[Lyn(U!1).Rec(b~Y!1,a), WORLD]} => "./react_data/LynRec.dat"
    //COUNT[Lyn(U!1).Rec(b~Y!1,a!2).Lig(l!2), WORLD]} => "./react_data/LynRecLig.dat"

    {COUNT[Rec(b~pY!?), WORLD] } => "./react_data/RecPbeta.dat"
    {COUNT[Rec(a!1).Lig(l!1,l), WORLD]} => "./react_data/RecMon.dat"
    {COUNT[Rec(a!1).Lig(l!1,l!2).Rec(a!2), WORLD]} => "./react_data/RecDim.dat"
    {COUNT[Lig(l!1,l!2).Lyn(U!3,SH2).Rec(a!2,b!3).Rec(a!1,b), WORLD]} => "./react_data/RecRecLigLyn.dat"

    {COUNT[Rec(g~pY),WORLD] + COUNT[Rec(g~pY!+), WORLD] } => "./react_data/RecPgamma.dat"
    {COUNT[Rec(g~pY!1).Syk(tSH2!1), WORLD] } => "./react_data/RecSyk.dat"
    {COUNT[Rec(g~pY!1).Syk(tSH2!1,a~pY), WORLD] } => "./react_data/RecSykPS.dat"

    //{COUNT[Syk, WORLD] } => "./react_data/SykTest.dat"
    //{COUNT[Lyn, WORLD] } => "./react_data/LynTest.dat"
    //{COUNT[Rec, WORLD] } => "./react_data/RecTest.dat"

}


INCLUDE_FILE = "fceri_mdl.viz_output.mdl"

/*
REACTION_DATA_OUTPUT
{
  STEP=1e-6
  {COUNT[volume_proxy,WORLD]}=> "./react_data/seed_00001/volume_proxy.World.dat"
  {COUNT[surface_proxy,WORLD]}=> "./react_data/seed_00001/surface_proxy.World.dat"
}
*/
